import "engine_sim.mr"
import "../part_library.mr"

units units()
constants constants()

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
}

public node lawnmower {
    alias output __out: engine;

    engine engine(
        name: "Lawnmower Engine",
        starter_torque: 200 * units.lb_ft,
        starter_speed: 1000 * units.rpm,
        redline: 5000 * units.rpm
    )

    wires wires()

    crankshaft c0(
        throw: 69 * units.mm / 2,
        flywheel_mass: 1.5 * units.lb,
        mass: 1 * units.lb,
        friction_torque: 1.0 * units.lb_ft,
        moment_of_inertia: 0.22986844776863666 * 0.5,
        position_x: 0.0,
        position_y: 0.0,
        tdc: -constants.pi / 4
    )

    rod_journal rj0(angle: 0.0)
    c0
        .add_rod_journal(rj0)

    piston_parameters piston_params(
        mass: 350 * units.g,
        //blowby: k_28inH2O(0.1),
        compression_height: 1.0 * units.inch,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    connecting_rod_parameters cr_params(
        mass: 250.0 * units.g,
        moment_of_inertia: 0.0015884918028487504,
        center_of_mass: 0.0,
        length: 4.0 * units.inch
    )

    cylinder_bank_parameters bank_params(
        bore: 75 * units.mm,
        deck_height: (4.0 + 1) * units.inch + 69 * units.mm / 2
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 10.0 * units.cm2,
        intake_flow_rate: k_carb(50.0),
        idle_flow_rate: k_carb(0.0),
        idle_throttle_plate_position: 0.95,
        throttle_gamma: 1.0
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(300.0),
        primary_tube_length: 10.0 * units.inch,
        primary_flow_rate: k_carb(200.0),
        velocity_decay: 1.0,
        volume: 1.0 * units.L
    )

    impulse_response ir0(filename: "../../smooth_39.wav", volume: 0.001)
    exhaust_system exhaust0(es_params, audio_volume: 1.0, impulse_response: ir0)

    cylinder_bank b0(bank_params, angle: 0 * units.deg)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire1
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    harmonic_cam_lobe lobe(
        duration_at_50_thou: 160 * units.deg,
        gamma: 1.1,
        lift: 5000 * units.thou,
        steps: 200
    )

    vtwin90_camshaft_builder camshaft(
        lobe_profile: lobe,
        lobe_separation: 114 * units.deg,
        base_radius: 500 * units.thou
    )

    b0.set_cylinder_head (
        generic_small_engine_head(
            chamber_volume: 50 * units.cc,
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 45 * units.deg)
        .add_sample(1000 * units.rpm, 45 * units.deg)
        .add_sample(2000 * units.rpm, 45 * units.deg)
        .add_sample(3000 * units.rpm, 50 * units.deg)
        .add_sample(4000 * units.rpm, 55 * units.deg)
		.add_sample(5000 * units.rpm, 60 * units.deg)
		.add_sample(6000 * units.rpm, 65 * units.deg)

    engine.add_ignition_module(
        vtwin90_distributor(
            wires: wires,
            timing_curve: timing_curve,
            rev_limit: 5500 * units.rpm
        ))
}
